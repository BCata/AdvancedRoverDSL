grammar mars.rover.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.rover.mars/DSL"

Mission:
	'MissionName:' name = ID
	('MasterMAC:' masterMAC = STRING)?
	'BorderColor:' borderColor = Color
	'GroundColor:' groundColor = Color
	'Timeout:' duration = Duration
	(createReport = 'CreateReport')?
	'Tasks:' tasks += Task+
;

Task:
	'Task:' name = Name
;

Duration:
	dl = INT unit = TimeUnit
;

Name:
	FindLakes | PushBricks | ParkRover | Celebrate 
;

FindLakes:
	'FindLakes'
	('Timeout:' (duration = Duration | 'untilTermination'))?
	lakes += Lake+
;

Lake:
	'Lake:'
		'Color:' color = Color
		('MeasureFeatures:' (features += Feature)+)?
;

Feature:
	'Feature:' type = FeatureType
;

PushBricks:
	'PushBricks'
	('Timeout:' (duration = Duration | 'untilTermination'))?
	'Amount:' number = INT
;

ParkRover:
	{ParkRover} 'ParkRover'
;

Celebrate:
	'Celebrate' 'with' celebrate = Celebration
;

enum Celebration:
	SPIN = 'spin' | 
	SING = 'sing'
;

enum FeatureType:
	TEMPERATURE = 'temperature' |
	DEPTH = 'depth' |
	SALINTY = 'salinity'
;

enum Color:
	WHITE = 'white' |
	BLACK = 'black' |
	RED = 'red' |
	BLUE = 'blue' |
	YELLOW = 'yellow' |
	GREEN = 'green'
;

enum TimeUnit:
	SECONDS = 'sec' |
	MINUTE = 'min' |
	HOURS = 'hrs'
;




	
